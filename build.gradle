allprojects{
	/* gradle.properties werden sowieso immer geladen*/
	/* libraries.gradle - manuell nachladen*/
	apply from: "${rootProject.projectDir}/libraries.gradle"
	
	repositories {
		mavenLocal()
		maven { 
			url "${repositoryUrl}" 
		}
    }
	
	/* Custimization part - e.g. when you got special repositories
	 * or an enterprise repo like artifactory or nexus */	 
	File f = new File(rootProject.projectDir,'custom_allprojects.gradle');
	if (f.exists()){
		apply from: "${rootProject.projectDir}/custom_allprojects.gradle"
	}
		
}

def doNotAutomateEclipseSetup = ['egradle-plugin','egradle-feature','egradle-updatesite'];

subprojects {
    apply plugin: 'java'
	
	if (! doNotAutomateEclipseSetup.contains(project.name)){
		apply plugin: 'eclipse'
	}
	
	apply plugin: "maven"
		
	group = "de.jcup.egradle" 
	version = "0.1"
	def buildNr = System.getenv('bamboo.buildNumber')
	// use build number in version
	if (buildNr != null && ! buildNr.isEmpty()){
		version= version + "_"+buildNr
	}
		
	/* Setup UTF-8 for compile AND test compilation*/
	[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

	sourceCompatibility = JavaVersion.VERSION_1_8
		
	dependencies{
			
		testCompile library.junit
		testCompile library.mockito_all
	}
	
	 /* per default GRADLE stops the build if one single test fails ...*/
    test { 
        ignoreFailures = true 
    }
	
	task sourcesJar(type: Jar, dependsOn:classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn:javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
	}


}

def our_group='egradle'

/* own task for complete project with sub projects...*/
task testReportAll(type: TestReport) {
	description 'Starts all tests for all sub projects and cumulate the results into build folder of root project.'
	group our_group
    subprojects {
        test {
            /* stops single reports for each project */
            reports.html.enabled = false
        }
    }
    destinationDir = file("$buildDir/reports/allUnitTests")

    /* report on the results of the subprojects' test task*/
    reportOn subprojects*.test
}


