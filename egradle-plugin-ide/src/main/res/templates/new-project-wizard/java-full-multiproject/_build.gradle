/** 
 * This build file was intially generated by EGradle
 * by using '#{egradle.template.name}' template
 * 
 */
def projectsNotInEclipse = [/*'no-eclipse-project1','no-eclipse-project2'*/]; 

allprojects{
	
	/* Include libraries.gradle - will support variable "library" where each child project is able to add
	 * dependencies by the global variables, instead of doing this always again - see subprojects closure
	 * below where junit and mockito are added this way to all sub projects*/ 
	apply from: "${rootProject.projectDir}/libraries.gradle"
	
	/* Turn off doclint java 8 */
	if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
	}
	
	/* define repositories*/
	repositories {
		/* add your local maven repo - so you can deploy there */
		mavenLocal() 
		/* add maven central */
		mavenCentral()
    }

	/* 
	 * Custimization part - e.g. when you got special repositories
	 * or an enterprise repo like artifactory or nexus where you do NOT
	 * want to expose URL to git. The file is intially ignored by git.
	 */	 
	File f = new File(rootProject.projectDir,'custom_allprojects.gradle');
	if (f.exists()){
		apply from: "${rootProject.projectDir}/custom_allprojects.gradle"
	}
}

/*
 * Define sub projects. The root project is NOT defined as an eclipse project, but all
 * other sub projects will be (except those being mentioned in projectsNotInEclipse).
 * EGradle will import all subprojects automatically then.
 */
subprojects {
    apply plugin: 'java'
	apply plugin: 'maven'
	
	if (! projectsNotInEclipse.contains(project.name)){
		apply plugin: 'eclipse'
	} 
		
	group = "#{egradle.template.groupname}"
	version = "0.0.1"
	
	/* Continous integration part - use build number in version when 'ci.buildNumber' is set*/
	def buildNr = System.getenv('ci.buildNumber')
	if (buildNr != null && ! buildNr.isEmpty()){
		version= version + "_"+buildNr
	}
		
	/* Setup UTF-8 for compile AND test compilation*/
	[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

	sourceCompatibility = '#{egradle.template.java.version}'
		
	/* Default dependencies for all sub projects. */
	dependencies{
			
		testCompile library.junit
		testCompile library.mockito_all
	}
	
	/* Per default GRADLE stops the build if one single test fails. We want to have all tests executed. */
    test { 
        ignoreFailures = false 
    }
	
	/**
	 * Task to create source jars
	 */
	task sourcesJar(type: Jar, dependsOn:classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

    /**
	 * Task to create javadoc jars
	 */
	task javadocJar(type: Jar, dependsOn:javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	/**
	 * Define artifacts
	 */
	artifacts {
		archives sourcesJar
		archives javadocJar
	}


}

